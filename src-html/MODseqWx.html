<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre><span align="left">.noeqin
<span align="left">.include	GEOSequates
.glbl
.eqin

<span align="left">;****************************************
;* MODseqW - geoProjects library (C) 1998 Bo Zimmerman
<span align="left">;*
;* Intro:
;* 	This modules supports C= seq/prg file
;* 	creation and writing.  A write buffer
;*	and directory entries will be maintained
;*	during execution.
;*
;* Functions:
;*	CreatSF(	.word	Filename pointer
;*			.byte	Splat file type) *
;*	PutSByte(	.a	Byte to write out) *
;*	PutSString(	.word	String pointer) *
;*	PutSBuf(	.word	Buffer pointer
;*			.word	Number of bytes) *
;*	PutSMem(	.word	Buffer pointer
;*			.word	Last byte pointer) *
;*	ClosSF(		.byte	File type to save as) *
;*	CpyFName(	A2	Pointer to filename
;*			A3	Destination pointer)
;*	MakeFNUpp( 	A2	Pointer to filename)
;*
;*	* = includes i_Function call, and returns 
;*		status in the .x register
;*
;* Comments:
;* 	A multi file version will have to await
<span align="left">;* 	some thought to the multiple-dir block
;* 	problem (two files sharing a dir block).
;*	There is also the problem of shared 
;*	BAM space between different disks.
;* 	In the meantime, consider a MODseqWx
;*	that has swap buffers equal in size to
;*	the ramsects in this module.  A Swap-
;*	Drive function would restore settings 
;*	from the buffer, allowing files on sepa-
;*	rate drives at least.
;****************************************
.psect

<span align="left">;******************************
;* CreatSF	Create a new c= sequential file
;* Inputs:
;*		A2	pointer to a file name
;*		A3L	file type (splat type)
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	0
;******************************

CreatSF:	
	jsr	MakeFNUpp	; all c= filenames uppercase ONLY
	mvw	A2, R6	; copy filename pointer for GEOS
	ldb	$886e,#1
	jsr	FindFile	; ensure file doesn't already exist
	cpx	#5	; file not found only good error
	beq	11$
	cpx	#0	; zero means file exists
	bne	10$
	ldx	#63	; so change it to 63--file exists
10$:	lda	#0	; two obligatory bytes
	rts		; exit in early disgrace
11$:	jsr	OpenDisk	; no errors yet, so read in BAM
	cpx	#0
	bne	10$	; a bad disk error, so exit
	ldw	R2,#3	; allocate 3 bytes to get a new block
	ldy	#0
	ldw	R6,#APITemp1
	jsr	BlkAlloc
	cpx	#0	; allocated, so check for errors
	bne	10$	; another bad disk error! Exit!
	mvw	APITemp1,SIOWTnS; restore saved t&s list
	jsr	SIOWiniBlk	; clear out the given sector
	ldb	R10L,#0
	jsr	GetFreeDirBlk	; now find a directory entry
	cpx	#0
	bne	10$	; a error getting dir entry!
	mvw	R1,SIOWdirTS	; OK! now save the dir block tk & sec
	tya		; prepare index into dir blk
	jsr	SIOWiniDir	; create pointer, copy dir block
	mvb	A3L,APITemp1	; save the file type
	mvw	SIOWdirPtr,A3	; copy work dir pointer
	mvw	SIOWdirPtr,R1
	ldw	R0,#$1d	; clear some space in dir block
	jsr	ClearRam
	ldy	#0	; now init the dir entry
	lda	APITemp1	; set as unclosed PRG file
	sta	(A3),y	; write into dir entry
	iny
	lda	SIOWTnS	; pointer to first track
	sta	(A3),y	; write into dir entry
	iny
	lda	SIOWTnS+1	; pointer to first sector
	sta	(A3),y	; write into dir entry
	ldw	SIOWFsize,#1	; init the file size
	avw	#3,A3	; set up for filename copy
	jsr	CpyFname	; copy filename A2 into space A3
	jsr	SIOWSvBAM	; save this disks BAM map
	mvw	SIOWdirTnS,R1	; restore dir block Tk & sec
	ldw	R4,#SIOWdirBlk
	jsr	PutBlock	; save the directory entry back
	cpx	#0
	bne	99$
	lda	#0
99$:	rts

<span align="left">;******************************
;* i_CreatSF	Create a new c= sequential file
;* Inputs:
;*		.word	pointer to a file name
;*		.byte	file type (splat type, 0=PRG)
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	0
;******************************

i_CreatSF:
	jsr	APIinit
	lda	A3L	; check splat type
	beq	10$
	ldb	A3L,#2	; default file type is #2 (PRG)
10$:	jsr	CreatSF
	ldy	#3	; three byte parameter list
	jmp	APIreturn

<span align="left">;******************************
;* PutSByte	Output a byte to a sequential file
;* Inputs:
;*		.a	byte to write out
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;******************************

PutSByte:
	inc	SIOWBuf+1	; increment byte pointer
	beq	11$	; exit on wrap over
	ldy	SIOWBuf+1	; otherwise write the byte
	sta	SIOWBuf,y
	ldx	#0	; clear errors and exit
10$:	rts		; no new block must be written
11$:	sta	APITemp1		; save byte for later
	jsr	SIOWLdBAM	; reload this disks BAM
	avw	#1,SIOWFsize	; increment file size
	ldw	R2,#3	; 3 bytes is all thats needed...
	ldw	R6,#APITemp2	; to allocate 1 new block
	jsr	BlkAlloc
	cpx	#0	; now check for errors
	bne	10$	; an error? yes = to the exit
	jsr	SIOWSvBAM	; save the updated BAM 
	mvw	APITemp2,SIOWBuf; clean, so create link
	mvw	SIOWTnS,R1	; restore this blocks Tk & Sec
	ldw	R4,#SIOWBuf	; and ready it for writing
	jsr	PutBlock	; the block is written!
	cpx	#0	; was there an error on that write?
	bne	10$	; if so, then exit
	mvw	APITemp2,SIOWTnS; now save the new Tk and Sec
	jsr	SIOWiniBlk	; re-clear the buffer block
	lda	APITemp1	; restore the byte to write
	jmp	PutSByte	; and write it out


<span align="left">;******************************
;* i_PutSByte	Output a byte to a sequential file
;* Inputs:
;*		.byte	byte to write out
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	byte written
;******************************

i_PutSByte:
	jsr	APIinit
	lda	A2L
	jsr	PutSByte
	ldy	#1
	lda	A2L
	jmp	APIreturn

<span align="left">;******************************
;* PutSString	Output a string to a sequential file
;* Inputs:
;*		A2	pointer to $ to write out
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;* Destroyed:
;*		A3	ptr to string Null
;******************************

PutSString:
	mvw	A2,A3	; create a work pointer
SIOWSS2:	ldy	#0
	lda	(A3),y	; get a byte
	beq	11$	; if NULL, end
	jsr	PutSByte	; otherwise write it out
	cpx	#0	; check for disk error
	bne	11$	; if error, also exit
	inc	A3L	; otherwise increment work pointer
	bne	10$	; if low byte no zeroed, skip
	inc	A3H	; increment high byte of wk ptr
10$:	jmp	SIOWSS2	; go write the next byte
11$:	rts

<span align="left">;******************************
;* i_PutSString	Output a string to a sequential file
;* Inputs:
;*		.word	pointer to $ to write out
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;******************************

i_PutSString:
	jsr	APIinit
	jsr	PutSString
	ldy	#2
	jmp	APIreturn

<span align="left">;******************************
;* PutSBuf	Output a buffer to a seq file
;* Inputs:
;*		A2	pointer to buffer
;*		A3	number of bytes to write 
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;* Destroyed:
;*		A3	converted to last byte
;******************************

PutSBuf:	adw	A2,A3	; convert to PutSMem
	dec	A3L	; now lose the extra byte
	cbi	A3L,#255
	bne	10$
	dec	A3H
10$:	jmp	PutSMem

<span align="left">;******************************
;* i_PutSBuf	Output a buffer to seq file
;* Inputs:
;*		.word	pointer to buffer
;*		.word	number of bytes to write 
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;* Destroyed:
;******************************

i_PutSBuf:
	jsr	APIinit
	jsr	PutSBuf
	ldy	#4
	jmp	APIreturn

<span align="left">;******************************
;* PutSMem	Output a block of memory to a seq file
;* Inputs:
;*		A2	pointer to start location
;*		A3	pointer to last byte 
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;* Destroyed:
;*		A2	will point to end of buffer
;******************************

PutSMem:
10$:	ldy	#0
	lda	(A2),y	; get the byte
	jsr	PutSByte	; write it out
	cpx	#0	; check for an error
	beq	12$	; if error found, then exit
	cpw	A2,A3	; now see if end is reached
	beq	12$	; if yes, then exit
	inc	A2L	; otherwise increment A2
	bne	11$	; no low byte overflow?
	inc	A2H	; low byte overflow means inc A2H
11$:	bra	10$	; and write the next byte
12$:	rts

<span align="left">;******************************
;* i_PutSMem	Output a block of memory to seq file
;* Inputs:
;*		.word	pointer to start location
;*		.word	pointer to last byte 
;*		curDrive	drive to create file on
;* Outputs:	
;*		.x 	status
;*		.a	?
;* Destroyed:
;******************************

i_PutSMem:
	jsr	APIinit
	jsr	PutSMem
	ldy	#4
	jmp	APIreturn

<span align="left">;******************************
;* ClosSF	Close a file opened with CreatSF
;* Inputs:
;*		A2L	file type to close as
;*		curDrive	drive file created on
;* Outputs:	
;*		.x 	status
;*		.a	0
;******************************

ClosSF:
	mvw	SIOWTnS,R1	; restore last tk & sec
	ldw	R4,#SIOWBuf	; and get the last block
	jsr	PutClock	; write the block out
	cpx	#0	; an error is checked for
	bne	11$	; if error found, skip rest
	mvw	SIOWdirPtr,A4	; restore directory pointer
	ldy	#0
	lda	A2L	; get the file type ready
	sta	(A4),y	; save the file type
	ldy	#$1c	; ready the file size index
	lda	SIOWFsize	; get the file size low byte
	sta	(A4),y	; and write it to the buffer
	iny		; prepare the file size index+1
	lda	SIOWFsize+1	; get the file size high byte
	sta	(A4),y	; and write it to the buffer
	mvw	SIOWdirTnS,R1	; restore dir block tk and sec
	ldw	R4,#SIOdirBlk	; prepare the dir block itself
	jsr	PutBlock	; write it to disk
	cpx	#0	; an error on that write?
	bne	11$	; if so, then exit
	jsr	SIOWLdBAM	; restore the BAM block
	jsr	PutDirHead	; and send the BAM back to disk
11$:	lda	#0
	rts


<span align="left">;******************************
;* i_ClosSF	Close a file opened with CreatSF
;* Inputs:
;*		.byte	file type (0=PRG)
;*		curDrive	drive file created on
;* Outputs:	
;*		.x 	status
;*		.a	0
;******************************

i_ClosSF:
	jsr	APIinit
	lda	A2L
	bne	10$
	ldb	A2L,#130
10$:	jsr	ClosSF
	ldy	#1
	jmp	APIreturn

<span align="left">;******************************
;* CpyFName	Copy a filename A2->A3
;* Inputs:
;*		A2	file name to copy
;*		A3	destination for filename
;* Outputs:	
;*		.a	?
;******************************

CpyFname:
	ldy	#$ff	; prepare for filename copy
10$:	iny
	lda	(A2),y	; copy filename chars
	sta	(A3),y
	beq	11$	; keep going until NULL,
	cmp	#$A0	; or a pad character is found.
	bne	10$
11$:	cpy	#16	; now, until all 16 chars filled in...
	beq	12$
	lda	#$A0	; ...pad with these damn things
	sta	(A3),y	
	iny
	bne	11$
12$:	rts

<span align="left">;******************************
;* MakeFNUpp	Make a filename uppercase
;* Inputs:
;*		A2	file name to change
;* Outputs:	
;*		.a	?
;******************************

MakeFNUpp:
	ldy	#$ff
10$:	iny		; loop through filename chars
	lda	(A2),y
	beq	11$	; loop until NULL char...
	cmp	#$A0	; ...or a pad character is found
	beq	11$
	cmp	#'a'	; otherwise look at the byte
	blt	10$	; if greater than a...
	cmp	#'z'	; ...and less = z...
	bgt	10$	; then its lowercase!
	sec
	sbc	#'a' - 'A'	; now fix it
	sta	(A2),y	; and save it back
	bra	10$
11$:	rts

<span align="left">SIOWSvBAM:
	ldy	#$00
10$:	lda	$9c80,y
	sta	SIOWBAM,y
	iny
	bne	10$
	rts

SIOWLdBAM:
	ldy	#$00
10$:	lda	SIOWBAM,y
	sta	$9c80,y
	iny
	bne	10$
	rts

<span align="left">SIOWiniBlk:
	ldw	R0,#256
	ldw	R3,#SIOWBuf
	jsr	ClearRam
	inc	BlkBuf+1	; initialize the number of bytes
	rts

SIOWiniDir:
	clc		; first, create pointer to block
	adc	#[(SIOWdirBlk)	; assume .a has index
	sta	SIOWdirPtr	
	lda	#0
	adc	#](SIOWdirBlk)	; and try to handle carrys
	sta	SIOWdirPtr+1
	ldy	#0
10$:	lda	$8000,y	; now copy block from $8000
	sta	SIOWdirBlk,y	; to our own block buffer
	iny
	bne	10$	; copy until done
	rts

<span align="left">.ramsect

SIOWBuf:	.block	256
SIOWdirBlk:	.block	256
SIOWBAM:	.block	256
SIOWdirTS:	.block	2
SIOWFsize:	.block	2
SIOWdirPtr:	.block	2
SIOWTnS:	.block	2</pre></body></html>